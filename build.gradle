import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version "$jfxPlugin"
    id 'com.vanniktech.maven.publish' version "$mavenPublish"
}

group = 'io.github.palexdev'
version = "$ver"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation("io.github.palexdev:scenicview:$scenicView") {
        exclude(group: "org.openjfx")
    }
    testImplementation "org.testfx:testfx-junit5:$testfx"
    testImplementation platform("org.junit:junit-bom:$junit")
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

java {
    sourceCompatibility = "$jdk"
    targetCompatibility = "$jdk"
    withSourcesJar()
    withJavadocJar()
    tasks.withType(Jar).each { it.archiveBaseName.set("rectcut") }
}

javafx {
    version = "$jfx"
    modules = ['javafx.controls']
    configurations = ['testImplementation']
}

test {
    // Because the new TestFX is garbage :)
    jvmArgs += [
            '--add-opens', 'javafx.graphics/com.sun.javafx.application=ALL-UNNAMED'
    ]
    useJUnitPlatform()
}

configurations {
    // Remove vanniktech non-sense
    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(plainJavadocJar)) {
            plainJavadocJar.enabled = false
        }
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.S01)
    signAllPublications()
}